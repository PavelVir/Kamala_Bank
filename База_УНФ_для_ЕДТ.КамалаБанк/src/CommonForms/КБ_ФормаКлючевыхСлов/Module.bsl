#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Истина Тогда // По типу конфигурации
		Налог = Справочники.ВидыНалогов.ПустаяСсылка();
		Сотрудник = Справочники.Сотрудники.ПустаяСсылка();
		Статья = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
		Счет = ПланыСчетов.Управленческий.ПустаяСсылка();
	КонецЕсли;

	УстановитРеквизитыПоПараметрам();
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОк(Команда)
	ЗаписатьКлючевоеСловоОбъекта();
	Закрыть();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитРеквизитыПоПараметрам()

	Для Каждого ЭлементФормы Из ЭтаФорма.ПолучитьРеквизиты() Цикл
		Если Параметры.Свойство(ЭлементФормы.Имя) Тогда
			ЭтаФорма[ЭлементФормы.Имя] = Параметры[ЭлементФормы.Имя];
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры
&НаСервере
Процедура ЗаписатьКлючевоеСловоОбъекта()

	Если Не ЗначениеЗаполнено(КлючевоеСлово) Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВидОперации) Тогда
		РегистрыСведений.КБ_КлючевыеСловаОбъектов.ЗаписатьКлючевоеСловоОбъекта(КлючевоеСлово, ВидОперации);
	КонецЕсли;

	Если ЗначениеЗаполнено(Счет) Тогда
		РегистрыСведений.КБ_КлючевыеСловаОбъектов.ЗаписатьКлючевоеСловоОбъекта(КлючевоеСлово, , Счет);
	КонецЕсли;

	Если ЗначениеЗаполнено(Сотрудник) Тогда
		РегистрыСведений.КБ_КлючевыеСловаОбъектов.ЗаписатьКлючевоеСловоОбъекта(КлючевоеСлово, , Сотрудник);
	КонецЕсли;

	Если ЗначениеЗаполнено(Статья) Тогда
		РегистрыСведений.КБ_КлючевыеСловаОбъектов.ЗаписатьКлючевоеСловоОбъекта(КлючевоеСлово, , Статья);
	КонецЕсли;

	Если ЗначениеЗаполнено(Налог) Тогда
		РегистрыСведений.КБ_КлючевыеСловаОбъектов.ЗаписатьКлючевоеСловоОбъекта(КлючевоеСлово, , Налог);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()

	флСотр = Ложь;
	ФлНалоги = Ложь;
	
	//@skip-warning
	Если ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Зарплата Или ВидОперации
		= Перечисления.ВидыОперацийРасходСоСчета.Подотчетнику Тогда
		флСотр = Истина;
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Налоги Тогда
		ФлНалоги = Истина;
	Иначе
		// ?
	КонецЕсли;
	Элементы.Сотрудник.Видимость = флСотр;
	Элементы.Налог.Видимость = ФлНалоги;

КонецПроцедуры

&НаКлиенте
Процедура КлючевоеСловоПриИзменении(Элемент)
	Если Не ЗначениеЗаполнено(КлючевоеСлово) Тогда
		Возврат;
	КонецЕсли;

	лРезультатКво = РезультатСоответствийИзСтроки(КлючевоеСлово);

	Если лРезультатКво = 0 Тогда
		Возврат;
	КонецЕсли;

	оооЗ = Новый ОписаниеОповещения("КлючевоеСловоПриИзмененииОтвет", ЭтаФорма);
	ПоказатьВопрос(оооЗ, НСтр("ru = 'По указанной строке уже есть соответствия, хотите загрузить их в форму?'"),
		РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаСервере
Функция РезультатСоответствийИзСтроки(Знач Текст)

	лСписок = РегистрыСведений.КБ_КлючевыеСловаОбъектов.СписокОбъектовПоКлючевомуСлову(Текст);
	АдресХранилища = ПоместитьВоВременноеХранилище(лСписок, УникальныйИдентификатор);
	Возврат лСписок.Количество();

КонецФункции

&НаКлиенте
Процедура КлючевоеСловоПриИзмененииОтвет(Ответ, Параметры) Экспорт

	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	КлючевоеСловоПриИзмененииОтвет_Сервер();

КонецПроцедуры

&НаСервере
Процедура КлючевоеСловоПриИзмененииОтвет_Сервер()

	лПараметры = ПолучитьИзВременногоХранилища(АдресХранилища);

	лАналитикиДокумента = ЗаполняемыеАналитики(); // ИД объекта (справочник идентиф.) + имя реквизита документа

	//@skip-warning
	Для Каждого СтрокаАналитики Из лПараметры Цикл

		Если Не ЗначениеЗаполнено(СтрокаАналитики.Значение) Тогда
			Продолжить;
		КонецЕсли;

		лИмяАналитики = лАналитикиДокумента.Получить(СтрокаАналитики.ИдентификаторОбъекта);

		Если лИмяАналитики <> Неопределено Тогда
			ЭтаФорма[лИмяАналитики] = СтрокаАналитики.Значение;
		КонецЕсли;

	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполняемыеАналитики()

	// Для УНФ
	лРезультат = Новый Соответствие;

	лРезультат.Вставить(ИдОбъекта(Справочники.ВидыНалогов.ПустаяСсылка()), "Налог");
	лРезультат.Вставить(ИдОбъекта(Справочники.Сотрудники.ПустаяСсылка()), "Сотрудник");
	лРезультат.Вставить(ИдОбъекта(Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка()), "Статья");
	лРезультат.Вставить(ИдОбъекта(ПланыСчетов.Управленческий.ПустаяСсылка()), "Счет");

	Возврат лРезультат;

КонецФункции

&НаСервереБезКонтекста
Функция ИдОбъекта(пСсылка)
	Возврат ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипЗнч(пСсылка));
КонецФункции
#КонецОбласти